group 'com.hadihariri'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.0.4'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

task createTestResources << {
    def mockMakerFile = new File("$projectDir/src/test/resources/mockito-extensions/org.mockito.plugins.MockMaker")
    if (System.env.MOCK_MAKER != null) {
        logger.info("Using MockMaker ${System.env.MOCK_MAKER}")
        mockMakerFile.parentFile.mkdirs()
        mockMakerFile.createNewFile()
        mockMakerFile.write(System.env.MOCK_MAKER)
    } else {
        logger.info("Using default MockMaker")
    }
}

apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'kotlin'


repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.mockito:mockito-core:2.1.0"
    testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0-M2")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0-M2")
}
